# Edited for DART - AHQ 2012-08-07

cmake_minimum_required (VERSION 2.6)
project (grip_all)

message (STATUS "Creating Makefile for the GRIP project")

SET(CPACK_PACKAGE_VERSION_MAJOR "2")
SET(CPACK_PACKAGE_VERSION_MINOR "0")
SET(CPACK_PACKAGE_VERSION_PATCH "0")
SET(CPACK_PACKAGE_NAME "grip")
SET(CPACK_DEBIAN_PACKAGE_DEPENDS "dart (>= 2.0), libwxgtk2.8-dev (>= 2.8)")

#####################################
# Global Variables
set (CMAKE_VERBOSE_MAKEFILE OFF)

if(WIN32)
	set (CMAKE_INSTALL_PREFIX "C:/Golems" CACHE PATH "Install prefix" FORCE)
    message (STATUS "Setup Visual Studio Specific Flags")
	set (CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG} /MDd /Zi /GL /Gy /W1 /EHsc")
	set (CMAKE_CXX_FLAGS_RELEASE "${CMAKE_CXX_FLAGS_RELEASE} /MD /Zi /GL /Gy /W1 /EHsc")
	set (CMAKE_DEBUG_POSTFIX "d")
	set (CMAKE_EXE_LINKER_FLAGS "/LTCG")
	set (CMAKE_MODULE_LINKER_FLAGS "/LTCG")
else ()
   set (CMAKE_INSTALL_PREFIX /usr )
   set (CMAKE_CXX_FLAGS "-g -O0 -Wall -Wextra -Wpointer-arith -fno-default-inline -fno-implicit-inline-templates -DwxUSE_THREADS -std=c++0x -fpermissive")
endif()

include (${CMAKE_INSTALL_PREFIX}/share/dart/DARTConfig.cmake)

INCLUDE_DIRECTORIES( ${DART_INCLUDE_DIRS} )
LINK_DIRECTORIES( ${DART_LIBRARY_DIRS} )


#####################################
include_directories (src)
add_subdirectory (src)

set (srcs src/GRIP.cpp)
set (hdrs src/GRIPApp.h)

if(WIN32)
  set (wxWidgets_EXCLUDE_COMMON_LIBRARIES TRUE)
  set (wxWidgets_USE_LIBS base core gl png tiff jpeg zlib)
else(WIN32)
  set (wxWidgets_USE_LIBS base core gl)
endif(WIN32)
find_package (wxWidgets)
if (wxWidgets_FOUND) 
  include (${wxWidgets_USE_FILE})
  add_library (grip ${srcs} ${hdrs})
  target_link_libraries (grip ggui gtools gtabs )
  target_link_libraries (grip ${DART_LIBRARIES} )
  set_target_properties (grip PROPERTIES ARCHIVE_OUTPUT_DIRECTORY "${CMAKE_SOURCE_DIR}/lib")
  install (FILES ${hdrs} DESTINATION include/grip)
  install (TARGETS grip ARCHIVE DESTINATION lib)
else (wxWidgets_FOUND)
  message ("wxWidgets not found!")
endif (wxWidgets_FOUND)

#####################################
# Create config file
SET(GRIP_CONFIG ${CMAKE_SOURCE_DIR}/GRIP_config.cmake)
message(STATUS "Generating ${GRIP_CONFIG}")
FILE( WRITE ${GRIP_CONFIG} "# This file is automatically generated from CMake\n")
FILE(APPEND ${GRIP_CONFIG} "# Include this file in CMakeLists.txt with \n# INCLUDE(\${CMAKE_INSTALL_PREFIX}/share/dart/GRIP_CONFIG.cmake) \n\n")
FILE(APPEND ${GRIP_CONFIG} "\nINCLUDE_DIRECTORIES(${CMAKE_INSTALL_PREFIX}/include)")
FILE(APPEND ${GRIP_CONFIG} "\nINCLUDE_DIRECTORIES(${CMAKE_INSTALL_PREFIX}/include/grip)")
FILE(APPEND ${GRIP_CONFIG} "\nINCLUDE_DIRECTORIES(${CMAKE_INSTALL_PREFIX}/include/fcl)")
FILE(APPEND ${GRIP_CONFIG} "\nSET(GRIP_HEADERS ${CMAKE_INSTALL_PREFIX}/include/grip)")
if(UNIX)
    FILE(APPEND ${GRIP_CONFIG} "\nINCLUDE_DIRECTORIES(${CMAKE_INSTALL_PREFIX}/include/eigen3)")
endif()
FILE(APPEND ${GRIP_CONFIG} "\n\nLINK_DIRECTORIES(${CMAKE_INSTALL_PREFIX}/lib)")
FILE(APPEND ${GRIP_CONFIG} "\nSET(GRIP_LIBDIR ${CMAKE_INSTALL_PREFIX}/lib)")
FILE(APPEND ${GRIP_CONFIG} "\nSET(GRIP_LIBRARIES optimized grip optimized ggui optimized gtabs optimized gtools debug gripd debug gguid debug gtabsd debug gtoolsd )")
FILE(APPEND ${GRIP_CONFIG} "\n\n")
INSTALL(FILES ${GRIP_CONFIG} DESTINATION share/grip)

# Add an "uninstall" target
CONFIGURE_FILE ("${PROJECT_SOURCE_DIR}/cmake/uninstall_target.cmake.in" "${PROJECT_BINARY_DIR}/uninstall_target.cmake" IMMEDIATE @ONLY)
ADD_CUSTOM_TARGET (uninstall "${CMAKE_COMMAND}" -P "${PROJECT_BINARY_DIR}/uninstall_target.cmake")

#####################################
# Build Projects
set (GRIP_HEADERS ${CMAKE_SOURCE_DIR}/src)
set (GRIP_LIBRARIES grip ggui gtabs gtools ${DART_LIBRARIES})
set (GRIP_LIBDIR ${CMAKE_SOURCE_DIR}/lib)
add_subdirectory (projects/tabEmpty)
add_subdirectory (projects/tabTemplate)

#####################################
# Package Installer

SET(CPACK_PACKAGE_VERSION "${CPACK_PACKAGE_VERSION_MAJOR}.${CPACK_PACKAGE_VERSION_MINOR}.${CPACK_PACKAGE_VERSION_PATCH}")
SET(CPACK_PACKAGE_FILE_NAME "${CPACK_PACKAGE_NAME}-${CPACK_PACKAGE_VERSION}-${CPACK_SYSTEM_NAME}")
SET(CPACK_DEBIAN_PACKAGE_MAINTAINER "Golems (http://github.com/golems/)")

SET(CPACK_PACKAGE_DESCRIPTION_SUMMARY "GRIP is a robotics simulator based on the DART physics engine.")
SET(CPACK_PACKAGE_VENDOR "Humanoid Robotics Lab at GT")

INCLUDE(CPack)


